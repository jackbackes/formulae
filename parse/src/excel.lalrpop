use std::str::FromStr;
use crate::ast::{Expr, Opcode, Error}; 

grammar;

Tier<Op,NextTier>: Box<Expr> = {
    Tier<Op,NextTier> Op NextTier => Box::new(Expr::Op(<>)),
    NextTier
};

pub Expr = Tier<ExprOp, Factor>;
Factor = Tier<FactorOp, SingleComparison>;
SingleComparison = Tier<SingleComparisonOp, MultiComparison>; 
MultiComparison = Tier<MultiComparisonOp, Term>; 

ExprOp: Opcode = { 
    "+" => Opcode::Add,
    "-" => Opcode::Subtract,
};

FactorOp: Opcode = {
    "*" => Opcode::Multiply,
    "/" => Opcode::Divide,
	"^" => Opcode::Exponent,
	"&" => Opcode::Concat,
	"%" => Opcode::Percent,
};

SingleComparisonOp: Opcode = {
	"=" => Opcode::Equal, 
	"<" => Opcode::LessThan,
	">" => Opcode::GreaterThan,
}; 

MultiComparisonOp: Opcode = {
	"<>" => Opcode::NotEqual,
	"<=" => Opcode::LessThanOrEqual,
	">=" => Opcode::GreatThanOrEqual 
}; 

Err: Error = {
	"#NULL!" => Error::Null, 
	"#DIV/0!" => Error::Div, 
	"#VALUE!" => Error::Value, 
	"#REF!" => Error::Ref, 
	"#NAME?" => Error::Name,
	"#NUM!" => Error::Num, 
	"#N/A" => Error::NA, 
	"#GETTING_DATA" => Error::GettingData 
}

Term: Box<Expr> = {
    Num => Box::new(Expr::Num(<>)),
	Err => Box::new(Expr::Error(<>)), 
	Cell => Box::new(Expr::Cell(<>)), 
    "(" <Expr> ")"
};

Cell: String = {
	r"(('[^/\\?*\[\]]{1,31}'|[A-Za-z0-9_]{1,31})!)?((\$?[A-Za-z]{1,3})(\$?[0-9]{1,6}))" => (<>).to_string()
}

Num: i32 = {
    r"[0-9]+" => i32::from_str(<>).unwrap()
};
